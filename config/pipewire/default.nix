{ config, lib, pkgs, ... }:
with lib;
let
  cfg = config.custom.pipewire;
in
{

  options = {
    custom.pipewire.enable = mkEnableOption "Pipewire setup";
    custom.pipewire.bluetooth = mkOption {
      type = types.bool;
      default = true;
    };
  };

  config = mkIf cfg.enable {

    hardware.bluetooth.enable = mkIf cfg.bluetooth true;
    security.rtkit.enable = true;
    services.pipewire = {
      enable = true;
      alsa.enable = true;
      alsa.support32Bit = true;
      pulse.enable = true;
      media-session.config.bluez-monitor.rules = mkIf cfg.bluetooth [
        {
          # Matches all cards
          matches = [{ "device.name" = "~bluez_card.*"; }];
          actions = {
            "update-props" = {
              "bluez5.reconnect-profiles" = [ "hfp_hf" "hsp_hs" "a2dp_sink" ];
              # mSBC is not expected to work on all headset + adapter combinations.
              "bluez5.msbc-support" = true;
              # SBC-XQ is not expected to work on all headset + adapter combinations.
              "bluez5.sbc-xq-support" = true;
            };
          };
        }
        {
          matches = [
            # Matches all sources
            { "node.name" = "~bluez_input.*"; }
            # Matches all outputs
            { "node.name" = "~bluez_output.*"; }
          ];
          actions = {
            "node.pause-on-idle" = false;
          };
        }
      ];
    };
  };

}
